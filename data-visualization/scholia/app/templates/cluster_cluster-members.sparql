# tool: scholia
# Prolific authors for a specific project
SELECT ?count ?author ?authorLabel ?orcid ?example_work ?example_workLabel
WITH {
  SELECT DISTINCT ?work WHERE {
    ?work wdt:P50 ?author .
    ?author wdt:P463 wd:{{ q }} .
  }
} AS %works
WITH {
  SELECT
    ?author
    (COUNT(?work) AS ?count)
    (SAMPLE(?work) AS ?example_work)
  WHERE {
    INCLUDE %works
    ?work wdt:P50 ?author .    
    ?author wdt:P463 wd:{{ q }} .
  }
  GROUP BY ?author
} AS %result
WHERE {
  INCLUDE %result
          
  # Include optional ORCID iD
  OPTIONAL { ?author wdt:P496 ?orcid . }
  
  # Label the results
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" . }
}
ORDER BY DESC(?count)
LIMIT 50
